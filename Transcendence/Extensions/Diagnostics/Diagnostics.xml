<?xml version="1.0" ?>

<!DOCTYPE TranscendenceExtension
	[
	<!ENTITY unidExtension				"0xA0010000">
	<!ENTITY unidCoverImage				"0xA0010001">
	<!ENTITY unidDiagnosticsCompatible	"0xA0010002">
	<!ENTITY unidItemUnitTests			"0xA0010003">
	<!ENTITY unidRPGUnitTests			"0xA0010004">
	<!ENTITY unidTopologyTests			"0xA0010005">
	<!ENTITY unidMissionUnitTests		"0xA0010006">
	<!ENTITY unidItemEnhancementUnitTests	"0xA0010007">

	<!-- 0040-007F TEST ITEMS -->

	<!ENTITY itTestDriveDualSlot		"0xA0010040">
	<!ENTITY itTestArmorScaled			"0xA0010041">
	<!ENTITY itTestWeaponScaled			"0xA0010042">
	<!ENTITY itTestWeaponRelativistic	"0xA0010043">
	<!ENTITY itTestWeaponSideMounted	"0xA0010044">
	<!ENTITY itTestWeaponExternal		"0xA0010045">
	<!ENTITY itTestWeaponFading			"0xA0010046">
	<!ENTITY itTestRareInNewBeyond1		"0xA0010047">
	<!ENTITY itTestRareInNewBeyond2		"0xA0010048">

	<!-- 0080-00BF TEST FUNCTIONS -->

	<!ENTITY unidCCComparisonTests		"0xA0010080">
	<!ENTITY unidLanguageTests			"0xA0010081">
	<!ENTITY unidMathTests				"0xA0010082">
	<!ENTITY unidCCStringTests			"0xA0010083">
	<!ENTITY unidCCLogicalOperators		"0xA0010084">
	<!ENTITY unidSystemFunctionTests	"0xA0010085">
	<!ENTITY unidCCListTests			"0xA0010086">

	<!-- 00C0-00FF TEST MISSIONS -->

	<!ENTITY msTestMission01			"0xA00100C0">
	<!ENTITY msTestMission02			"0xA00100C1">
	<!ENTITY msTestMission03			"0xA00100C2">
	<!ENTITY msTestMissionArc01			"0xA00100C3">
	<!ENTITY msTestMissionArc02			"0xA00100C4">
	<!ENTITY msTestMissionArc03			"0xA00100C5">

	<!-- 0100-013F TEST DESIGN TYPES -->

	<!ENTITY unidDesignTypeTests		"0xA0010100">
	<!ENTITY unidTestBaseType			"0xA0010101">
	<!ENTITY unidTestDescendantType1	"0xA0010102">
	<!ENTITY unidTestDescendantType2	"0xA0010103">
	<!ENTITY unidTestStationType		"0xA0010104">
	<!ENTITY unidTestArmorClass			"0xA0010105">
	<!ENTITY stTestStation1				"0xA0010106">
	<!ENTITY stTestStation2				"0xA0010107">

	<!-- 0140-017F TEST OVERLAYS -->

	<!ENTITY unidTestOverlays			"0xA0010140">
	<!ENTITY ovTestOverlay01			"0xA0010141">
	<!ENTITY ovTestOverlay02			"0xA0010142">

	<!-- 0180-01CF TEST TSE -->

	<!ENTITY unidTestTSE				"0xA0010180">

	<!-- OTHER -->

	<!ENTITY unidStarsOfThePilgrim		"0x00200000">
	]>

<TranscendenceExtension UNID="&unidExtension;" 
		name=			"Diagnostics"
		release=		"1"
		version=		"1.0.1"
		apiVersion=		"45"
		extends=		"&unidStarsOfThePilgrim;"
		debugOnly=		"true"
		coverImageID=	"&unidCoverImage;"
		>

	<Library unid="&unidRPGLibrary;"/>
	<Library unid="&unidHumanSpaceLibrary;"/>

	<Module filename="DesignTypeTests.xml"/>
	<Module filename="DockScreenTests.xml"/>
	<Module filename="FunctionTests.xml"/>
	<Module filename="ItemEnhancementUnitTests.xml"/>
	<Module filename="ItemUnitTests.xml"/>
	<Module filename="MissionUnitTests.xml"/>
	<Module filename="RPGUnitTests.xml"/>
	<Module filename="SystemFunctionTests.xml"/>
	<Module filename="TestArmorClasses.xml"/>
	<Module filename="TestItems.xml"/>
	<Module filename="TestMissions.xml"/>
	<Module filename="TestOverlays.xml"/>
	<Module filename="TestStationTypes.xml"/>
	<Module filename="TestTSE.xml"/>
	<Module filename="TopologyTests.xml"/>

<!-- DIAGNOSTICS -->

	<Type unid="&unidDiagnosticsCompatible;">
		<Events>
			<OnGlobalStartDiagnostics>
				(block ()
					(diagRun (list
						(lambda () (sysSelfTest))
						))

					(diagTestAutonBay)
					(diagTestDeviceSlots)
					(diagTestMeteorsteel)
					)
			</OnGlobalStartDiagnostics>

			<OnGlobalSystemDiagnostics>
				(block ()
					(diagTestStationItemInstall)

					; (diagDebugOreTable)
					)
			</OnGlobalSystemDiagnostics>
		</Events>
	</Type>

	<Globals>
	(block ()
		;	TESTS --------------------------------------------------------------

		(setq diagTestAutonBay (lambda ()
			(block ()

				;	Initialize some vars

				(setq gScreen Nil)

				;	Run tests

				(diagRun (list

					;	Create ship

					(lambda () (diagCreatePlayerShip))

					;	Create an auton

					(lambda ()
						(setq autonObj (sysCreateShip &sc1MBattleAuton; (objGetPos gPlayerShip) &svCommonwealth;))
						)

					;	Install a weapon

					(lambda ()
						(block (result)

							(setq result (rpgInstallDevicePrep {
								item: (itmCreate &itBallistaCannon; 1)
								targetObj: autonObj
								installerObj: gPlayerShip
								totalPrice: 0
								noCargoCheck: True
								}))

							(if (not (@ result 'canInstall))
								(error (@ result 'desc))
								)
							)
						)

					;	Done with the player ship and auton

					(lambda () 
						(block ()
							(objDestroy gPlayerShip)
							(setq gPlayerShip Nil)

							(objDestroy autonObj)
							(setq autonObj Nil)
							)
						)
					))
				)
			))

		(setq diagTestDeviceSlots (lambda ()
			(block ()
				(diagRun (list

					;	Create ship

					(lambda () (diagCreatePlayerShip))

					;	Install weapons to fill all slots

					(lambda () 
						(for i 1 4
							(diagInstallItem gPlayerShip (itmCreate &itLaserCannon; 1))
							)
						)

					;	Install a drive

					(lambda () 
						(diagInstallItem gPlayerShip (itmCreate &itTritiumPropulsionUpgrade; 1))
						)

					;	Swap in a 2-slot drive. This should fail because
					;	we do not have enough slots.

					(lambda () 
						(diagInstallItem gPlayerShip (itmCreate &itTestDriveDualSlot; 1) { result:'noDeviceSlotsLeft })
						)

					;	Remove one of the laser cannons

					(lambda ()
						(shpRemoveDevice gPlayerShip (@ (objGetItems gPlayerShip "wI") 0))
						)

					;	Now swap the 2-slot drive (this should work)

					(lambda () 
						(diagInstallItem gPlayerShip (itmCreate &itTestDriveDualSlot; 1))
						)

					;	Done with the player ship

					(lambda () 
						(block ()
							(objDestroy gPlayerShip)
							(setq gPlayerShip Nil)
							)
						)
					))
				)
			))

		(setq diagTestEnhanceCombine (lambda (itemType enhance1 enhance2)
			(block (
				result1 result2
				)
				(if (= enhance1 'damaged)
					(objAddItem gPlayerShip (itmSet@ itemType 'damaged True))

					(block ()
						(objAddItem gPlayerShip itemType)
						
						(setq result1 
							(objEnhanceItem gPlayerShip 
								(@ (objGetItems gPlayerShip (cat "*U +unid:" itemType ";")) 0)
								enhance1
								)
							)
						(if (and (!= (@ result1 'resultCode) 'ok) (!= (@ result1 'resultCode) 'defectOK))
							(error "Unexpected result: " result1)
							)
						)
					)

				(setq result2
					(objEnhanceItem gPlayerShip 
						(@ (objGetItems gPlayerShip (cat "*U +unid:" itemType ";")) 0)
						enhance2
						)
					)

				(objRemoveItem gPlayerShip (@ (objGetItems gPlayerShip (cat "*U +unid:" itemType ";")) 0))

				result2
				)
			))

		(setq diagTestMeteorsteel (lambda ()
			(block ()
				(diagRun (list

					;	Create ship

					(lambda () (diagCreatePlayerShip))

					;	Install meteorsteel armor

					(lambda () 
						(diagInstallItem gPlayerShip (itmCreate &itLightMeteorsteel; 1))
						)

					;	See if shields are disabled

					(lambda ()
						(block (
							(shieldItem (@ (objGetItems gPlayerShip "sI") 0))
							)
							(if (not (objGetCondition gPlayerShip 'shieldBlocked))
								(error "Shield not disabled by meteorsteel")
								)
							)
						)

					;	Done with the player ship

					(lambda () 
						(block ()
							(objDestroy gPlayerShip)
							(setq gPlayerShip Nil)
							)
						)
					))
				)
			))

		(setq diagTestStationItemInstall (lambda ()
			(block (theItem)

				;	Initialize some vars

				(setq gScreen Nil)

				;	Create a ship that we can use as a player ship

				(diagRun (list
					(lambda () (diagCreatePlayerShip))
					))

				;	Run a series of tests for every friendly station

				(enum (sysFindObject gPlayerShip "TAF") theStation
					(diagRun (list
							
						;	See if the station can install a random device on the 
						;	player ship.
						;	(But skip Commonwealth Fortress and Black Market because
						;	you need rank to get buy stuff.)

						(if (and (objHasTradeService theStation 'InstallDevice)
								(objMatches theStation gPlayerShip "* -commonwealthArmory; -blackMarket;")
								(setq theItem (random (objGetItems theStation "d~rUN")))
								)
							(lambda ()
								(block (
									result
									)
									(setq result (rpgInstallDevicePrep {
										item: theItem
										installerObj: theStation
										targetObj: gPlayerShip
										upgrade: True
										}))

									(switch
										;	If we succeed in installing, then OK.

										(@ result 'canInstall)
											True

										;	If we're trying to install the same item, then we can
										;	skip this.

										(itmIsEqual theItem (@ result 'itemToReplace) 'ignoreInstalled)
											True

										;	Otherwise, installation failed for some reason.

										(block ()
											(print (objGetName theStation) ": " (@ result 'desc))
											)
										)
									)
								)
							)
						))
					)

				;	Done with player ship

				(objDestroy gPlayerShip)
				(if gPlayerShip
					(error "gPlayerShip not cleared")
					)
				)
			))

		;	DEBUGGING ----------------------------------------------------------

		(setq diagDebugOreTable (lambda ()
			(block (oreTable totalOrePrice oreCount)

				(sysGetRandomOreType)
				(setq oreTable (sysGetData "08040003_oreTable"))

				(setq oreCount (- (count oreTable) 1))
				(setq aveOrePrice
					(map oreTable '(excludeNil reduceAverage) theEntry
						(if (@ theEntry 1)
							(itmGetPrice (itmCreate (@ theEntry 1) 1))
							)
						)
					)

				(print "System " (sysGetNode) " Level " (sysGetLevel) " Count " oreCount " Average Price " aveOrePrice)
				(print "-----------------------------------------------------------")
				(print (join
					(map oreTable 'excludeNil theEntry
						(if (@ theEntry 1)
							(itmGetName (itmCreate (@ theEntry 1) (@ theEntry 0)) '(short count))
							)
						)
					'oxfordComma
					))
				(print " ")
				)
			))

		;	HELPERS ------------------------------------------------------------

		(setq diagCreatePlayerShip (lambda (options)
			(block (playerObj)
				(setq playerObj (sysCreateShip &scSapphirePlayer; Nil &svPlayer;))
				(if (not playerObj)
					(error "Unable to create player ship.")
					)
				(shpOrder playerObj 'hold)

				;	Add the biggest reactor

				(objAddItem playerObj (itmCreate &it1GWReactor; 1))
				(shpInstallDevice playerObj (itmCreate &it1GWReactor; 1))
				(shpRefuelFromItem playerObj (itmCreate &itHadronVacuumFuelCell; 100))
				(objAddItem playerObj (itmCreate &itMilitaryID; 1))

				;	Add money

				(objCredit playerObj 1000000)
				(objCredit playerObj 'rin 1000000)

				;	Set gPlayerShip

				(diagSetPlayerShip playerObj)

				;	Return the ship

				playerObj
				)
			))

		(setq diagInstallItem (lambda (theShip theItem options)
			(block (
				(expectedResult (@ options 'result))
				(result (objCanInstallItem theShip theItem))
				)
				(switch
					(and expectedResult (not (= expectedResult (@ result 1))))
						(error (cat "Result: " (@ result 1) " [expected " expectedResult "]"))

					(and (not expectedResult) (not (@ result 0)))
						(error (cat "Cannot install: " (@ result 1)))

					(@ result 0)
						(block (
							(itemCat (itmGetProperty theItem 'category))
							installedItem
							)
							(objAddItem theShip theItem)

							(switch
								(= itemCat 'armor)
									(setq installedItem (shpInstallArmor theShip theItem (or (@ options 'armorSeg) 0)))

								(setq installedItem (shpInstallDevice theShip theItem))
								)

							(if (not (itmGetProperty installedItem 'installed))
								(error (cat "Installation failed: " (itmGetName installedItem 0) "."))
								)
							)
					)
				)
			))
		)
	</Globals>

<!-- RESOURCES -->
	
	<Image UNID="&unidCoverImage;"	bitmap="DiagnosticsCover.jpg" loadOnUse="true" />
	
</TranscendenceExtension>
