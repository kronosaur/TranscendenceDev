<?xml version="1.0" encoding="utf-8"?>

<TranscendenceModule>

<!-- MEMBERSHIP

	This module defines some helpers for implementing membership in an organization
	with experience points and ranks.

	REQUIREMENTS

	The membership type (usually the sovereign) must have the following elements:

	rankTable (Definition): We expect this to be a struct (or list) of ranks, indexed
		by rank, and containing an xpNeeded field. The rank indexing may be by either
		integer or string. If indexing by string, then nextRank field is required and
		and intial rank must be set (either in the property or with rpgRankInit)

		rank:		(Currently ignored) Commonly used to provide short text name for
					integer based ranks
		nextRank:	Index of the next rank. Required if rank is a string, otherwise will
					default to current rank + 1
		xpNeeded:	Experience points required to reach this rank. Optional if membership
					provides a CheckPromotion event
		condition:	Optional lambda that should return True if the player can be promoted
		icon:		Icon for rank details display

	rpg.descPromotion: This series of language elements should define the
		text shown to the player when they are promoted to a given rank. The rank is
		passed in gData in the rank field.

	DATA

	rpg.rank: We store the player's current rank.

	rpg.xp: This is the player's current XP.

	EVENTS

	CheckPromotion: If present this even can provide custom promotion criteria to
		be used instead of XP.

	OnPromotion: This event will be called from the promotion screen. Allowing additional
		rewards (e.g. military ID) to be given to the player. The event may also return a
		nextScreen struct if a follow on dockscreen should be displayed after the promotion

	NOTES

		If the player has no rank in a given membership (i.e. rpg.rank is Nil) and does not
		provide custom promotion code via CheckPromotion. Then the first successful mission
		will trigger a promotion to rank 0. This may be used to display a "welcome new pilot"
		screen AFTER the first successful mission.
		However, in most cases we will want to display the welcome message before the first
		mission. In this case the player rank should be initialised by calling rpgRankInit
		during the introduction screen.

-->

<!-- DOCK SCREENS -->

	<DockScreen unid="&dsRPGPromotion;"
			inherit=			"&dsDockScreenBase;"
			nestedScreen=		"true"
			>
		<Display type="detailsPane">
			<OnDisplayInit>
				(or (@ gData 'rankDetails) (objTranslate gSource 'rpg.statusDetails))
			</OnDisplayInit>
		</Display>

		<Panes>
			<Default>
				<OnPaneInit>
					(block (
						(membership (@ gData 'membership))
						(newRank (rpgIsPromoted membership))
						)

						(typSet@ membership 'rpg.rank newRank)
						(scrSetDesc gScreen (typTranslate membership "rpg.descPromotion" {
							sourceObj: gSource
							membership: membership
							rank: newRank
							}))
						)
				</OnPaneInit>

				<Actions>
					<Action id="actionContinue">
						(block (screenOptions)
							(setq screenOptions (typFireEvent (@ gData 'membership) 'OnPromotion))
							(scrExitScreen gScreen)
							(if (@ screenOptions 'nextScreen)
								(scrShowScreen gScreen (@ screenOptions 'nextScreen) (@ screenOptions 'nextScreenData))
								)
							)
					</Action>
				</Actions>
			</Default>
		</Panes>
	</DockScreen>

<!-- GLOBALS -->

	<Globals>
	(block ()
		(setq rpgIsPromoted (lambda (membership)
			"Check if the player should be promoted, returning their new rank (or Nil)"
			(block (
				(rank (or (typ@ membership 'rpg.rank) 0))
				(xp (or (typ@ membership 'rpg.xp) 0))
				(rankTable (typ@ membership 'rankTable))
				(nextRank (or (@ (@ rankTable rank) 'nextRank) (+ rank 1)))
				(nextRankDesc (@ rankTable nextRank))
				conditionCode
				)

				(switch
					;	Use the custom event if it exists

					(typHasEvent membership 'CheckPromotion)
						(typFireEvent membership 'CheckPromotion)

					;	If we previously had no rank (Nil) then promote to rank zero

					(not (typ@ membership 'rpg.rank))
						rank

					;	If no rank table of if we're already at the highest
					;	rank, then no promotion.

					(not nextRankDesc)
						Nil

					;	If we don't have enough XP, then no promotion.

					(ls xp (@ nextRankDesc 'xpNeeded))
						Nil

					;	If we have an additional condition lambda, promote if it returns True

					(setq conditionCode (@ nextRankDesc 'condition))
						(if (conditionCode)
							nextRank
							)

					;	Promotion to the next rank.

					nextRank
					)
				)
			))

		(setq rpgMissionRewardXP (lambda (membership xpReward missionObj)
			(block Nil

				;	Increment XP

				(typInc@ membership 'rpg.xp xpReward)

				;	If we're debriefing a mission, and we've been promoted,
				;	then set the appropriate promotion screen.

				(if (and missionObj 
						(rpgIsPromoted membership)
						)

					;	dsRPGMission checks for a well-known data member in the mission
					;	object to see if it should navigate to an additional screen.

					(block Nil
						(msnSetData missionObj 'rewardNextScreen &dsRPGPromotion;)
						(msnSetData missionObj 'rewardNextScreenData {
							membership: membership
							missionObj: missionObj
							})
						)
					)
				)
			))
		
		(setq rpgRankInit (lambda (theType defaultRank)
			"Initialize the given membership by setting rpg.rank if it has not already been set."
			(if (not (typ@ theType 'rpg.rank))
				(typSet@ theType 'rpg.rank (or defaultRank 0))
				)
			))

		(setq rpgRankNoun (lambda (theType rank)
			(typTranslate
				theType
				(cat "rank." (or rank (typ@ theType 'rpg.rank)))
				{
					gender:(plyGetGenome gPlayer)
				})
			))
		
		(setq rpgRankAchievements (lambda (theType)
			(if (typ@ theType 'rpg.rank)
				(list
					(list
						(typTranslate theType 'rpg.rankTypeDesc)
						(fmtNoun (typ@ theType 'rankNoun) 1 'titleCapitalize)
						)
					)
				)
			))
		)
	</Globals>

</TranscendenceModule>