<?xml version="1.0" encoding="utf-8"?>

<TranscendenceModule>

<!-- RPG Characters (NPCs)======================================================

	Station characters:
		Assign random character names to specific station roles (station master,
		bartender etc.) using rpgCharacterRandom. These can be used to make minor
		customisations to mission text. Example - Korolov Shipping

		Add variant property "rpgCharacter.assistantDirector" to station:
			(rpgCharacterRandom gSource "x +assistantDirector; +korolov;" 'assistantDirector)

		This will find suitable types with matching attributes and Static data "CharacterTable"

		Implementation

		Static Data on character types: CharacterTable
		Alternative property rpg.characterTable is not fully implemented

		Global data on space object type:
			rpgCharacterList.<role>
			rpgCharacterNext.<role>

		Property on space object: rpgCharacter.<role>


	Mission giver characters:
		Variant property set on mission. Used to provide alternative mission text.
		Example - Commonwealth Mining, Commonwealth Volunteer missions

		Note - current examples used in Eridani with custom tlisp calls to
		typTranslate. But these could probably be reworked to fit the station
		character system and use rpgCharacterTranslate


	Major characters:
		Typically used by characters which have an associated ship and can assign
		missions across different systems. Example: Benedict, Kate Morgental

		The character type defines following data

		Static Data:
			MissionAttribute
			Name
			Sovereign

		Dynamic [legacy type Data]:
			char.firstMeetingOn [firstPlayerMeetingTime]
			char.latestMeetingOn [latestPlayerMeetingTime]
				[lastNodeID]
				[lastShipClass]
				[objID_<unid>]
				[lastOrders]
				[lastSystemPos]
				[lastKnown]
				[playerRel]
				[status]
				[screenData]

		Events:
			OnCharacterCanBeMet
-->

<!-- CHARACTER SCREEN ==========================================================

	USAGE

	The object (generally a ship) with this screen must set the character 
	parameter on the class.

	The object must also define the following language elements:

		core.firstIntro
		core.intro
		core.noMissions

	The character type must define the following static data elements:

		MissionAttribute: A string representing the attribute to use for all 
				missions that may be assigned by the character.

	The character type must define the following events:

		OnCharacterCanBeMet: If handled, the event may return True to indicate
				that the meeting should continue. Or the event may return a
				string to display declining the meeting.

-->

	<DockScreen UNID="&dsRPGCharacterDefault;"
			inherit=			"&dsDockScreenBase;"
			>
		<OnScreenInit>
			;	Make sure the player is allowed to talk to this character.

			(block (explanation)
				(switch
					;	If Nil, then the event is not handled so we continue meeting.

					(not (setq explanation (typFireObjEvent (objGetProperty gSource 'character) gSource 'OnCharacterCanBeMet)))
						True

					;	If True, then we are allowed to meet and continue.

					(eq explanation True)
						True

					;	Otherwise, we expect an explanation string.

					(scrShowScreen gScreen &dsRPGMessage; {
						desc: explanation
						nextScreen: 'forceUndock
						})
					)
				)
		</OnScreenInit>

		<Panes>
			<Default>
				<OnPaneInit>
					(block (
						(charType (objGetProperty gSource 'character))
						(firstMeeting (not (or (typ@ charType 'char.firstMeetingOn) (typGetData charType 'firstPlayerMeetingTime))))
						)

						(rpgPagePaneInit gScreen 

							(or (scrTranslate gScreen (if firstMeeting 'core.firstIntro 'core.intro)) 
								(scrTranslate gScreen (if firstMeeting 'FirstMeetingIntro 'MeetingIntro))
								)

							{
							actionDoneID: 'actionContinue

							onExitPane: (lambda (theScreen pageDesc options)
								(block (charType screenData)
									(setq charType (objGetProperty gSource 'character))

									;	We've met character

									(rpgCharacterSetMeeting charType)
							
									;	Next screen

									(setq screenData (typGetData charType 'screenData))
									(if screenData
							
										;	If a screen has been set, show it.
								
										(switch
								
											;	If we have a translateType and translateID, then translate
											;	[We need to do this sometimes if the data that we're passing has 
											;	non-serializable objects.]
									
											(@ screenData 'translateID)
												(scrShowScreen 
													gScreen 
													(@ screenData 'screen) 
													(typTranslate (@ screenData 'translateType) (@ screenData 'translateID))
													)
									
											;	Otherwise, we expect a screen.
									
											(scrShowScreen 
												gScreen 
												(@ screenData 'screen) 
												(@ screenData 'pane) 
												(@ screenData 'data)
												)
											)

										;	If no set screen, look for missions.

										(rpgCharacterShowMissionScreen gSource { 
											nextScreen: 'forceUndock
											})
										)
									)
								)
							})
						)
				</OnPaneInit>
			</Default>
		</Panes>
	</DockScreen>

<!-- GLOBAL FUNCTIONS ==========================================================

	USAGE

	These functions use the following global variables on character types:

	char.firstMeetingOn: Tick of player's first meeting with character.
			Nil if the player has never met face to face.

	char.latestMeetingOn: Tick of most recent meeting with character.

	lastNodeID: Character's last known position. Nil means unknown.

	lastSystemPos: Character's last known position in the lastNodeID system.

	objID: The objID of character's current ship. If Nil, then no ship has
			been created yet.

	playerRel: If Nil, character has never met the player. Positive numbers mean that
			character likes/trusts/respects the player. Negative numbers mean the
			opposite.

	screenData: This is a structure specifying the screen to show when the player
			interacts with the character. The struct has the following fields:

			screen: The screen to show
			pane: If non-Nil, the pane to navigate to
			data: If non-Nil, data to pass to the screen

			If this value is Nil, then we look for missions tagged with 
			character mission attribute (e.g., kateMorgentalMission).

-->

	<Globals>
		(block Nil
			(setq rpgCharacterAscend (lambda (charType)
				(block (
					(lastNodeID (typGetData charType 'lastNodeID))
					
					shipClassValue
					objID
					shipObj
					)
					
					(switch
						;	If lastNodeID doesn't equal this system, then nothing to do.
						
						(and lastNodeID (!= lastNodeID (sysGetNode)))
							Nil	;(dbgOutput (sysGetNode) ": lastNodeID = " lastNodeID)
							
						;	If no last ship class then we can't do anything because we don't know
						;	how to find the object ID.
						
						(not (setq shipClassValue (typGetData charType 'lastShipClass)))
							Nil
							
						;	Get the object ID from the character type and ship class
						
						(not (setq objID 
								(typGetData charType 
									(cat 'objID_ (int shipClassValue))
									)
								))
							Nil	;(dbgOutput (sysGetNode) ": Can't find object ID for " charType)
							
						;	If no ship or the ship is already ascended, then nothing to do
						
						(not (setq shipObj (objGetObjByID objID)))
							Nil	;(dbgOutput (sysGetNode) ": Can't find ship object")
							
						(obj@ shipObj 'ascended)
							Nil	;(dbgOutput (sysGetNode) ": Ship already ascended")

						;	If the ship is currently a player wingman, then do not ascend;
						;	the ship will follow the player.

						(obj@ shipObj 'playerWingman)
							Nil
							
						;	Otherwise, ascend the object
						
						(block (orders)
							;	Save the current order stack
							(for i 1 (shpGetOrderCount shipObj)
								(block Nil
									(lnkAppend orders
										{
											desc: (shpGetOrderDesc shipObj)
											objID: (objGetID (shpGetOrderTarget shipObj))
											}
										)
									(shpCancelOrder shipObj)
									)
								)
							(typSetData charType 'lastOrders orders)
							(typSetData charType 'lastSystemPos (objGetPos shipObj))
							(typSetData charType 'lastKnown (objGetProperty shipObj 'known))
							(sysAscendObject shipObj)
							; (dbgOutput (sysGetNode) ": Ship ascended: " (objGetID shipObj))
							)
						)
					)
				))
				
			(setq rpgCharacterChangeRel (lambda (charType event)
			
				; This function changes the character's relationship with the
				; player based on something the player did.
				;
				; The following events are valid:
				;
				; 'betrayal: The player betrayed the character in a fundamental
				;		way, generally by agreeing to pursuing a goal and then
				;		actively opposing that goal.
				;
				; 'opposition: The player acted against a major character goal,
				;		but did not necessarily betray the character.
				;
				; 'failure: The player failed in a task that the character was
				;		counting on, generally through incompetence rather than
				;		enmity.
				;
				; 'aid: The player helped the character in pursuit of a major
				;		goal.
				;
				; 'rescue: The player risked his/her life to save the character
				;		(or to save the life of someone the character cares
				;		about).
				
				(block (
					(curRel (or (typGetData charType 'playerRel) 'notSet))
					)
					
					(switch
						
						(eq event 'betrayal)
							(typSetData charType 'playerRel
								(@ {
									notSet: 'nemesis			;	First impression count for a lot.
									nemesis: 'nemesis
									dislikes: 'nemesis
									neutral: 'dislikes
									likes: 'neutral
									loyalty: 'nemesis			;	Betrayal hurts most if you feel loyal to someone.
									}
									curRel
									))
								
						(eq event 'opposition)
							(typSetData charType 'playerRel
								(@ {
									notSet: 'dislikes
									nemesis: 'nemesis
									dislikes: 'dislikes
									neutral: 'dislikes
									likes: 'neutral
									loyalty: (random (list		;	60% of the time we stay loyal
										'nemesis
										'dislikes
										'neutral
										'likes
										'loyalty
										'loyalty
										'loyalty
										'loyalty
										'loyalty
										'loyalty
										))
									}
									curRel
									))
						
						(eq event 'failure)
							(typSetData charType 'playerRel
								(@ {
									notSet: 'dislikes
									nemesis: 'nemesis
									dislikes: 'dislikes
									neutral: 'dislikes
									likes: 'neutral
									loyalty: 'loyalty
									}
									curRel
									))
								
						(eq event 'aid)
							(typSetData charType 'playerRel
								(@ {
									notSet: 'likes
									nemesis: 'nemesis
									dislikes: (if (leq (random 1 100) 50) 'neutral 'dislikes)
									neutral: 'likes
									likes: (if (leq (random 1 100) 20) 'loyalty 'likes)
									loyalty: 'loyalty
									}
									curRel
									))
								
						(eq event 'rescue)
							(typSetData charType 'playerRel
								(@ {
									notSet: 'loyalty
									nemesis: 'loyalty
									dislikes: 'likes
									neutral: 'likes
									likes: 'loyalty
									loyalty: 'loyalty
									}
									curRel
									))
						)
					)
				))

			(setq rpgCharacterCreateShip (lambda (charType shipClassUNID pos options)
				(block (
					;	We store a ship ID for every unique ship class. Some characters
					;	use multiple ships.
					
					(shipClassValue (if shipClassUNID shipClassUNID (typGetData charType 'lastShipClass)))
					(shipIDVar (cat 'objID_ (int shipClassValue)))
					(objID (typGetData charType shipIDVar))
					
					shipObj
					)

					(switch
						;	If ship for character does not exist, then we need to 
						;	create him.

						(not objID)
 							(block Nil
								(setq shipObj (sysCreateShip shipClassValue pos (typGetStaticData charType 'Sovereign)))
								(objSetProperty shipObj 'known (@ options 'setKnown))
								(typSetData charType 'lastNodeID (sysGetNode))
								(typSetData charType 'lastShipClass shipClassValue)
								(typSetData charType 'lastOrders Nil)
								(typSetData charType shipIDVar (objGetId shipObj))
								(typSetData charType 'screenData Nil)
								
								; (dbgOutput (sysGetNode) ": No obj ID; creating ship: " (objGetID shipObj))
								)
								
						;	If we can't find the object, then this is an error, but we
						;	create the ship anyway
						
						(not (setq shipObj (objGetObjByID objID)))
							(block Nil
								(printTo 'log "ERROR: Unable to find object ID: " objID ".")
								
								(setq shipObj (sysCreateShip shipClassValue pos (typGetStaticData charType 'Sovereign)))
								(objSetProperty shipObj 'known (@ options 'setKnown))
								(typSetData charType 'lastNodeID (sysGetNode))
								(typSetData charType 'lastShipClass shipClassValue)
								(typSetData charType 'lastOrders Nil)
								(typSetData charType shipIDVar (objGetId shipObj))
								(typSetData charType 'screenData Nil)
								
								; (dbgOutput (sysGetNode) ": Obj ID not found; creating ship: " (objGetID shipObj))
								)

						;	If the object is ascended, we descend it.

						(objGetProperty shipObj 'ascended)
							(block (isKnown)

								;	If ship has been here all along, then just place him in the
								;	original position.

								(if (eq (sysGetNode) (typGetData charType 'lastNodeID))
									(block Nil
										(setq pos (typGetData charType 'lastSystemPos))
										(setq isKnown (typGetData charType 'lastKnown))
										)
									
									;	If he was not here last, then we honor options

									(block Nil
										(setq isKnown (@ options 'setKnown))
										(typSetData charType 'lastOrders Nil)
										)
									)

								;	Descend him back to the system

								(setq shipObj (sysDescendObject objID pos))
								; (dbgOutput (sysGetNode) ": Descending ship: " (objGetID shipObj))
								
								;	If we could not descend, then something happened. Just create
								;	the character here.

								(if (not shipObj)
									(block Nil
										(setq shipObj (sysCreateShip shipClassValue pos (typGetStaticData charType 'Sovereign)))
										(typSetData charType shipIDVar (objGetID shipObj))
										)
									)
									
								;	Remember where we are
									
								(typSetData charType 'lastNodeID (sysGetNode))
								(typSetData charType 'lastShipClass shipClassValue)
								
								;	Set known
								
								(objSetProperty shipObj 'known isKnown)
									
								;	Done
								
								(typSetData charType 'screenData Nil)
								)
							
						;	Otherwise, we assume shipObj is valid

						)

					;	We always reenable docking, in case some previous code disabled it.

					(if shipObj
						(objSetProperty shipObj 'dockingEnabled True)
						)
						
					;	If requested, we repair all damage
					
					(if (@ options 'repairAll)
						(block Nil
							(intArmorRepairAll shipObj)
							(objSetProperty shipObj 'interiorHP (objGetProperty shipObj 'maxInteriorHP))
							)
						)

					;	Return character's ship

					shipObj
					)
				))

			(setq rpgCharacterGetQuickRel (lambda (charType)

				; This function returns the given character's relationship with 
				; the player. The function returns one of the following values:
				;
				; Nil: The player and the character have never met.
				;
				; 'favorable: The character is favorable towards the player.
				;		(i.e., rel is neutral, likes, or loyalty)
				;
				; 'unfavorable: The character is unfavorable towards the player.
				;		(i.e., rel is dislikes or nemesis)

				(block (rel)
					(setq rel (rpgCharacterGetRel charType))

					(switch
						(not (or (typ@ charType 'char.firstMeetingOn) (typGetData charType 'firstPlayerMeetingTime)))
							Nil

						(or (eq rel 'nemesis)
								(eq rel 'dislikes))
							'unfavorable

						'favorable
						)
					)
				))

			(setq rpgCharacterGetRel (lambda (charType)
				
				; This function returns the given character's relationship with 
				; the player. The function returns one of the following values:
				;
				; Nil: The player and the character have never met (or the
				;		character has not yet formed an opinion).
				;
				; 'nemesis: The character hates the player.
				;
				; 'dislikes: The character dislikes the player, but not enough
				;		to actively work against her.
				;
				; 'neutral: The character does not have an opinion about the
				;		player.
				;
				; 'likes: The character generally likes the player, but is not
				;		necessarily a close friend.
				;
				; 'loyalty: The character feels loyalty and friendship for the
				;		player.
				
				(typGetData charType 'playerRel)
				))

			(setq rpgCharacterGetStatus (lambda (charType)
				(typGetData charType 'status)
				))

			(setq rpgCharacterIsAliveAndWell (lambda (charType)
				(block (
					(status (typGetData charType 'status))
					)
					(switch
						;	Dead means dead
						
						(or (= status 'dead) 
								(= status 'destroyed) 
								(= status 'destroyedByPlayer)
								)
							Nil
							
						;	Otherwise, we're alive and well
						
						True
						)
					)
				))

			(setq rpgCharacterSetMeeting (lambda (charType)
				(if (typHasProperty charType 'char.firstMeetingOn)
					(block ()
						(typSet@ charType 'char.latestMeetingOn (unvGetTick))
						(if (not (typ@ charType 'char.firstMeetingOn))
							(typSet@ charType 'char.firstMeetingOn (unvGetTick))
							)
						)
					(block ()
						(typSetData charType 'latestPlayerMeetingTime (unvGetTick))
						(if (not (typGetData charType 'firstPlayerMeetingTime))
							(typSetData charType 'firstPlayerMeetingTime (unvGetTick))
							)
						)
					)
				))

			(setq rpgCharacterSetScreen (lambda (charType screenData)
				(typSetData charType 'screenData screenData)
				))
				
			(setq rpgCharacterSetStatus (lambda (charType newStatus)
			
				;	This function sets the status of the character. We support the following
				;	values:
				;
				;	Nil: Character is alive and well. No special status.
				;
				;	'dead: Character is permanently dead.

				(block (
					(oldStatus (typGetData charType 'status))
					)
					(switch				
						;	If the character is permanently dead, then we can't 
						;	change the status.

						(= oldStatus 'dead)
							Nil

						;	Set the status

						(typSetData charType 'status newStatus)
						)
					)
				))

			(setq rpgCharacterShowMissionScreen (lambda (missionSource options)
				
				; This function is used to offer players a new mission.
				;
				; CONTEXT
				;
				; This must be called from inside a dock screen with a valid
				; gScreen parameter.
				;
				; PARAMETERS
				;
				; missionSource: The object that is giving the mission, generally
				;		character's ship.
				;
				; options:
				;		nextScreen: Screen to show when we're done.

				(block (nextMissionTime theMission rank newMissions)

					(setq charType (objGetProperty missionSource 'character))
					(setq missionAttrib (cat " +" (typGetStaticData charType 'MissionAttribute) ";"))

					(switch

						;	If we have an active mission from this ship then we show it.

						(setq theMission (@ (msnFind missionSource (cat "aS" missionAttrib)) 0))
							(scrShowScreen gScreen &dsRPGMission;
								{
								missionObj: theMission
								nextScreen: (@ options 'nextScreen)
								})

						;	If we have an open mission available, return that

						(setq theMission (@ (msnFind missionSource (cat "oS" missionAttrib)) 0))
							(scrShowScreen gScreen &dsRPGMission;
								{
								missionObj: theMission
								nextScreen: (@ options 'nextScreen)
								})

						;	Make a list of available missions. 
						;	If we can't find anything, then we've got nothing.

						(not (setq newMissions (typFind (cat "n" missionAttrib))))
							(scrShowScreen gScreen &dsRPGMessage; {
								desc: (or (objTranslate missionSource 'core.noMissions) (objTranslate missionSource 'NoMissions))
								nextScreen: 'forceUndock
								})

						; Create a random mission from the list. If we succeed, then show the mission
						; screen.

						(setq theMission (msnCreate newMissions missionSource))
							(scrShowScreen gScreen &dsRPGMission;
								{
								missionObj: theMission
								nextScreen: (@ options 'nextScreen)
								})

						; Otherwise, nothing

						(scrShowScreen gScreen &dsRPGMessage; {
							desc: (or (objTranslate missionSource 'core.noMissions) (objTranslate missionSource 'NoMissions))
							nextScreen: 'forceUndock
							})
						)
					)
				))

			;	STATION CHARACTER PERSONAS -------------------------------------
			;
			;	This set of functions are used to manage random NPC on various
			;	stations. For example, these can be used to randomize a quest
			;	giver on a station.
			;
			;	USAGE
			;
			;	1.	Generate a CharacterTable in a StaticData block in one or 
			;		more types. A CharacterTable is a struct with one field per
			;		character ID. Each field is a struct containing information
			;		about the character.
			;
			;		For example:
			;
			;		{
			;			lukeSkywalker: {
			;				id: 'lukeSkywalker
			;				fullName: "Luke Skywalker"
			;				friendlyName: "Luke"
			;				formalName: "Skywalker"
			;				genome: 'human
			;				gender: 'genderMale
			;				notes: "Luke is a farmboy who dreams of being a pilot."
			;				}
			;			princessLeia: {
			;				id: 'princesLeia
			;				fullName: "Leia Organa"
			;				friendlyName: "Leia"
			;				formalName: "the Princess"
			;				genome: 'human
			;				gender: 'genderFemale
			;				notes: "Leia Organa is one of the leaders of the Rebellion."
			;				}
			;			}
			;
			;	2.	Call rpgCharacterAssignRandom in the OnCreate for a given
			;		station. You must call this once for each role used by the
			;		station. For example, if you have a station master and a
			;		bartender who both give out missions, you need two calls.
			;
			;	3.	Use rpgCharacterTranslate to get specific text for the 
			;		randomly chosen character.
			;
			;	4.	Use rpgCharacterGetInfo to get the full block of data for 
			;		the randomly chosen character.

			;	DEPRECATED. Assign sourceType charID in property

			(setq rpgCharacterAssign (lambda (sourceObj role sourceType charID)
				(block (
					(characterRole (or role "npc"))
					(characterVar (cat "rpgCharacter." characterRole))
					)
					(objSetData sourceObj characterVar (list sourceType charID))
					(if (not (rpgCharacterGetInfo sourceObj role))
						(error "No character found: " sourceType "/" charID)
						)
					)
				))

			;	DEPRECATED. Use rpgCharacterRandom in a variant property

			(setq rpgCharacterAssignRandom (lambda (sourceObj criteria role)
				
				;	This function assigns a random character to the given source object.
				;	It should be called once when the object is created.
				;
				;	Use rpgCharacterTranslate to get text for the character.

				(block (
					(characterRole (or role "npc"))
					(characterVar (cat "rpgCharacter." characterRole))

					(characterID (rpgCharacterRandom sourceObj criteria characterRole))
					)

					;	Store this character with the object.

					(objSetData sourceObj characterVar characterID)

					;	Done

					characterID
					)
				))

			(setq rpgCharacterGenerateList (lambda (criteria)
				
				;	This function returns a list of character identifiers for use
				;	by minor characters, such as station masters. The result consists
				;	of a list of character identifiers. Each identifier is a list
				;	with two values: an UNID and an ID within that type.
				;
				;	The order in the list is randomized.

				(shuffle
					(apply append
						(map (typFind criteria) 'excludeNil theType
							(block (
								(characterTable (typGetStaticData theType 'CharacterTable))
								)
								(if characterTable
									(map characterTable theCharacter
										(list theType (@ theCharacter 0))
										)

									;	If no character table, then the whole type is one
									;	character.

									(list (list theType Nil))
									)
								)
							)
						)
					)
				))

			(setq rpgCharacterGetInfo (lambda (sourceObj role defaultType)
				(block (
					(characterRole (or role "npc"))
					(characterVar (cat "rpgCharacter." characterRole))

					;	characterID is either a struct, which contains the 
					;	character info, or it is a list with two elements pointing
					;	to the character info. If the latter, the first element is
					;	the type containing the info and the second element is the
					;	id in the table.

					(characterID (or (objGetProperty sourceObj characterVar) (objGetData sourceObj characterVar)))
					)
					(switch
						;	If we have the character info, just return it, adding in a type
						;	so that we can translate text.

						(= (typeof characterID) 'struct)
							(struct characterID { sourceType:(objGetType sourceObj) })

						;	Otherwise, look up from the table

						(block (
							(theType (@ characterID 0))
							(theID (@ characterID 1))

							(characterTable (or (typGetProperty theType 'rpg.characterTable) (typGetStaticData theType 'CharacterTable)))
							)
							(switch
								(@ characterTable theID)
									(struct (@ characterTable theID) { sourceType:theType })

								defaultType
									(struct (@ (typGetStaticData defaultType 'CharacterTable) 'default) { sourceType:defaultType })
								)
							)
						)
					)
				))

			(setq rpgCharacterRandom (lambda (sourceObj criteria role)
				
				;	This function assigns a random character to the given source object.
				;	It should be called once when the object is created.
				;
				;	Use rpgCharacterTranslate to get text for the character.

				(block (
					(type (objGetType sourceObj))
					(characterRole (or role "npc"))

					(characterListVar (cat "rpgCharacterList." characterRole))
					(characterList (typGetData type characterListVar))

					(nextVar (cat "rpgCharacterNext." characterRole))
					(next (typGetData type nextVar))

					(characterVar (cat "rpgCharacter." characterRole))

					characterID
					)

					;	If we don't yet have a character list, then generate one.

					(if (not characterList)
						(block ()
							(setq characterList (rpgCharacterGenerateList criteria))
							(setq next 0)
							(typSetData type characterListVar characterList)
							(typSetData type nextVar 0)
							)
						)

					;	Pick a random character (the list has been shuffled, so we 
					;	just pick the next character on the list).

					(setq characterID (@ characterList (mod next (count characterList))))

					;	Next character

					(typIncData type nextVar)

					;	Done

					characterID
					)
				))

			(setq rpgCharacterTranslate (lambda (sourceObj role textID data)
				(block (
					(charInfo (rpgCharacterGetInfo sourceObj role))
					)
					(typTranslate (@ charInfo 'sourceType) (cat (@ charInfo 'id) "." textID) data)
					)
				))
			)
	</Globals>

</TranscendenceModule>
